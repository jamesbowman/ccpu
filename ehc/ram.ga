---------------------------- 008 ----------------------------
\ GPIO 1 is CE
\ GPIO 3 is WE
    @p a! @p
      io
      0b100000000000101110
    ! @p b!
      NORTH
: again
    @b call case
    jump  cmd_read
    jump  cmd_write
    jump  cmd_block

: case
  pop + push ;

: cmd_read
    @b 2/ call read
    !b jump again

: read    ( addr -- v )
    @p a! !
      EAST
    @p push
      1
    unext @p a!
      WEST
    @p ! @ ;
      @ !p

: cmd_write
    @b 2/ @p
      EAST
    a! ! 
    @p @p @p @p
      @p !
      0x1000
      @p !b
      WEST
    a! ! !
    ! @b !

    @p a! @p @p
      io
      0b100000000000101110
      0b100000000000101010
    ! @p push
      1           \ write delay
    unext !
    @p a!
      WEST
    @p !
      dup or !b
    jump again

: cmd_block
    @p push @b
      7
    2* unext
    dup call read
    push
: blockloop
    @p . +
      1
    dup call read
    2* 2* 2*
    2* 2* 2*
    2* 2* 2*
    push @p . +
      1
    dup call read
    pop or !b
    next blockloop
    jump again

---------------------------- 007 ----------------------------
: cold
    @p !b
      0x0000
    @p a!
      data
    jump EAST

---------------------------- 009 ----------------------------
WIRE(WEST, data)

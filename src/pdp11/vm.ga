include(ga144.hdr)

include(708out.hdr)

---------------------------- 608 ----------------------------
WIRE(SOUTH, NORTH)

---------------------------- 508 ----------------------------

  @p b!
    EAST
  @p a!
    WEST
: loop              \ main execution loop
  call fetch
: execute           \ T is an opcode. Execute it
  dup !
  call WEST
  jump loop

: _emit
  ! pop a! ;

: ea                \ ( opcode -- ea ) compute EA from low 6 bits of opcode
  dup @p and        \ handle 'deferred' modes if bit 3 set
    8
  if simple
  call  simple
  jump  ram@
: simple
  drop dup push @p  \ compute EA of the R field
    7
  and 2* pop @p     \ save the original field
    0b110000
  and 2/ 2/
  2/ 2/
  call  case
  ;                 \ Rn
  jump  postinc     \ (Rn)+
  jump  predec      \ -(Rn)
: indexed           \ X(Rn)

  call  fetch
  push  call ram@
  pop . +
  @p and ;
    0xffff

: predec \ ( rptr )
  call  ram@
  @p . +
    -2
  dup jump  ram!

: postinc
  call ram@   \ ( ea )
  dup @p . +
    2
: ram! ( v -- )
  @p !b !b ;
    @p ! . .

: ssdd              \ ( opcode -- src dst ) get parameters of double operand
  dup push 2/
  2/ 2/ 2/
  2/ 2/ call ea@
  pop
: ea@   \ ( sf -- v )
  call ea
: ram@ ( ea -- v )
  2/ @p !b
    @p a! @ !p
  !b @b ;

: fetch             \ fetch next word from PC by evaluating @(PC)+
  @p jump ea
    037

: case
  pop + push ;

: branch8           \ ( opcode -- disp ) extract branch target -256..254
  @p dup push
    8
  push
  2* unext 2*
  2/ unext ;

                    
: conditional       \ if T is zero, take the branch in R
  if notake
: take
  drop pop -        \ invert opcode so it executes as 'pseudo'
  jump execute

: rconditional      \ if T is nonzero, take the branch in R
  if take
: notake
  drop pop drop ;

: ea_pushpc         \ compute EA, push the current PC. For CALL
    call ea
    @p call ssdd    \ ssdd field for "R7,-(SP)"
      00746
    drop jump ram!

---------------------------- 507 ----------------------------
\ fetch instruction from b
\ search table
\   send 4 words if match
\ do nothing if not found

  @p b!
    EAST
: searcher
  @b @p a!
    table
: checkloop
  dup @+ if searcher
  and @+ or
  if match
  drop a @p +
    4
  a! jump checkloop
: match
  @p push
    3
  @+ !b unext
  jump searcher

: table

  0177700   \ JUMP
  0000100
    call ea
    @p call ram@
      14
    drop jump ram!

  0177700   \ MFPI - used as 'SNAP' for debug
  0006500
    call ea@
    a push @p
      NORTH
    a! jump snap

  0177700   \ CLR
  0005000
    call ea@
    dup or
    .
    jump ram!

  0177700   \ COM
  0005100
    call ea@
    -
    .
    jump ram!

  0177700   \ INC
  0005200
    call ea@
    @p . +
      1
    jump ram!


  0177700   \ DEC
  0005300
    call ea@
    @p . +
      -1
    jump ram!


  0177700   \ NEG
  0005400
    call ea@
    - @p . +
      1
    jump ram!

  0170000   \ ADD
  0060000
    call ssdd
    . +
    .
    jump ram!

  0
